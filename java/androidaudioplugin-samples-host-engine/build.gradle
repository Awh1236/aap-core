plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 29
        targetSdkVersion 30
        versionCode 1
        versionName "0.6.8"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                // https://github.com/google/prefab/blob/bccf5a6a75b67add30afbb6d4f7a7c50081d2d86/api/src/main/kotlin/com/google/prefab/api/Android.kt#L243
                arguments "-DANDROID_STL=c++_shared"
                cppFlags "-Werror"

                // Enable this section if you want to debug native code using AddressSanitizer
                //arguments "-DANDROID_ARM_MODE=arm", "-DANDROID_STL=c++_shared"
                //cppFlags "-fsanitize=address -fno-omit-frame-pointer"
            }
        }
    }
    // This should not be necessary in normal projects, but we specify c++_shared and thus
    // ODR hits us. We specify ANDROID_STL=c++_shared in favor of prefab (which is not enabled
    // yet but will be at some stage). This means, every AAP related project has to fix the
    // NDK version in use. It is not realistic when it becomes for public uses, but so far
    // we have to take this pill...
    ndkVersion "21.3.6528147"

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    // FIXME: enable these sections once we migrated to prefab.
    /*
    buildFeatures {
        prefab true
    }
    */
    packagingOptions {
        exclude '**libc++_shared.so'
    }
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                groupId = 'org.androidaudioplugin'
                version = android.defaultConfig.versionName
                from components.release
                artifactId = 'androidaudioplugin-samples-host-engine'
            }
            // Creates a Maven publication called "debug".
            debug(MavenPublication) {
                // Applies the component for the debug build variant.
                groupId = 'org.androidaudioplugin'
                version = android.defaultConfig.versionName
                from components.debug
                artifactId = 'androidaudioplugin-samples-host-engine'
            }
        }
        repositories {
            maven {
                def releasesRepoUrl = "$buildDir/repos/releases"
                def snapshotsRepoUrl = "$buildDir/repos/snapshots"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }
}

dependencies {

    implementation project(':androidaudioplugin')
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.3"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.3"
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    implementation "androidx.core:core-ktx:1.3.2"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

// Starting AGP 7.0.0-alpha05, AGP stopped caring build dependencies and it broke builds.
// This is a forcible workarounds to build libandroidaudioplugin.so in prior to referencing it.
gradle.projectsEvaluated {
    mergeDebugNativeLibs.dependsOn(rootProject.project("androidaudioplugin").mergeDebugNativeLibs)
    mergeReleaseNativeLibs.dependsOn(rootProject.project("androidaudioplugin").mergeReleaseNativeLibs)
}
