plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
    id 'signing'
}

android {
    compileSdk 30
    buildToolsVersion "30.0.3"
    ext.description = "AndroidAudioPlugin - MidiDeviceService support"

    defaultConfig {
        minSdk 29
        targetSdk 30
        versionCode 1
        versionName '0.6.15'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                // https://github.com/google/prefab/blob/bccf5a6a75b67add30afbb6d4f7a7c50081d2d86/api/src/main/kotlin/com/google/prefab/api/Android.kt#L243
                arguments "-DANDROID_STL=c++_shared"
                cppFlags "-Werror"
            }
        }
    }
    // This should not be necessary in normal projects, but we specify c++_shared and thus
    // ODR hits us. We specify ANDROID_STL=c++_shared in favor of prefab (which is not enabled
    // yet but will be at some stage). This means, every AAP related project has to fix the
    // NDK version in use. It is not realistic when it becomes for public uses, but so far
    // we have to take this pill...
    ndkVersion "21.3.6528147"

    buildTypes {
        debug {
            packagingOptions {
                doNotStrip "**/*.so"
            }
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            version "3.18.1"
            path "src/main/cpp/CMakeLists.txt"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
    }
    packagingOptions {
        exclude '**libc++_shared.so'
    }
}

apply from: "../publish-pom.gradle"

dependencies {
    implementation "androidx.core:core-ktx:1.5.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation project(':androidaudioplugin')
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}

// Starting AGP 7.0.0-alpha05, AGP stopped caring build dependencies and it broke builds.
// This is a forcible workarounds to build libandroidaudioplugin.so in prior to referencing it.
gradle.projectsEvaluated {
    tasks['buildCMakeDebug'].dependsOn(rootProject.project("androidaudioplugin").mergeDebugNativeLibs)
    // It is super awkward in AGP or Android Studio, but gradlew --tasks does not list
    // `buildCMakeRelease` while `buildCMakeDebug` is there!
    // Fortunately(?) we can rely on another awkwardness that we reference *debug* version of
    //  -;androidaudioplugin in CMakeLists.txt for target_link_directories() so we can skip
    //  release dependency here (technically we need debug build *always* happen before
    //  release builds, but it always seems to happen.
//    tasks['buildCMakeRelease'].dependsOn(rootProject.project("androidaudioplugin").mergeReleaseNativeLibs)
}
