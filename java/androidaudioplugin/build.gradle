
plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'org.jetbrains.dokka'
    id 'maven-publish'
}

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 29
        targetSdkVersion 30
        versionCode 1
        versionName "0.6.11"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'

        externalNativeBuild {
            cmake {
                // https://github.com/google/prefab/blob/bccf5a6a75b67add30afbb6d4f7a7c50081d2d86/api/src/main/kotlin/com/google/prefab/api/Android.kt#L243
                arguments "-DANDROID_STL=c++_shared"
                cppFlags "-Werror"
            }
        }
    }
    // This should not be necessary in normal projects, but we specify c++_shared and thus
    // ODR hits us. We specify ANDROID_STL=c++_shared in favor of prefab (which is not enabled
    // yet but will be at some stage). This means, every AAP related project has to fix the
    // NDK version in use. It is not realistic when it becomes for public uses, but so far
    // we have to take this pill...
    ndkVersion "21.3.6528147"

    buildTypes {
        debug {
            packagingOptions {
                doNotStrip "**/*.so"
            }
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        cmake {
            path "../../native/androidaudioplugin/CMakeLists.txt"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // FIXME: PREFAB: enable these sections once we migrate to prefab-based solution.
    /*
    buildFeatures {
        prefabPublishing true
    }
    // Workaround for https://issuetracker.google.com/issues/168777344#comment5
    packagingOptions {
        exclude '**.so'
    }
    prefab {
        androidaudioplugin {
            name 'androidaudioplugin'
            // WARNING: It's not working https://github.com/atsushieno/android-audio-plugin-framework/issues/57
            // There is no way to specify more than one include directory.
            // headers '../../native/androidaudioplugin/android/include'
            // FIXME: remove this dummy headers dir hack once https://issuetracker.google.com/issues/172105145 is supported.
            headers '../../dummy-prefab-headers/include'
        }
    }
    */
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                groupId = 'org.androidaudioplugin'
                version = android.defaultConfig.versionName
                // FIXME: "release build" is known as broken. See issue #68
                from components.debug
                artifactId = 'androidaudioplugin'
            }
            // Creates a Maven publication called "debug".
            debug(MavenPublication) {
                // Applies the component for the debug build variant.
                groupId = 'org.androidaudioplugin'
                version = android.defaultConfig.versionName
                from components.debug
                artifactId = 'androidaudioplugin'
            }
        }
        repositories {
            maven {
                def releasesRepoUrl = "$buildDir/repos/releases"
                def snapshotsRepoUrl = "$buildDir/repos/snapshots"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.3.2'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:core:1.3.0'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    implementation "androidx.core:core-ktx:1.3.2"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
