
plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'org.jetbrains.dokka-android'
    id 'maven-publish'
}

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 29
        targetSdkVersion 30
        versionCode 1
        versionName "0.6.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'

        externalNativeBuild {
            cmake {
                arguments "-DANDROID_STL=c++_shared"
                // Enable this section if you want to debug native code using AddressSanitizer
                //arguments "-DANDROID_ARM_MODE=arm", "-DANDROID_STL=c++_shared"
                //cppFlags "-fsanitize=address -fno-omit-frame-pointer"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        cmake {
            path "../../native/androidaudioplugin/CMakeLists.txt"
        }
    }
    ndkVersion '21.3.6528147'

    packagingOptions{
        // FIXME: (ASAN) looks like a hack, but -DANDROID_STL=c++_shared causes this https://stackoverflow.com/questions/48749876/more-than-one-file-was-found-with-os-independent-path-lib-armeabi-v7a-libopencv
        // pickFirst '*.so'
        doNotStrip "*/*/*.so" // debuggable!
    }

    dokka {
        outputFormat = 'html'
        outputDirectory = "$buildDir/../../../docs/javadoc"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        prefabPublishing true
    }
    // WARNING: It's not working https://github.com/atsushieno/android-audio-plugin-framework/issues/57
    prefab {
        androidaudioplugin {
            name 'androidaudioplugin'
            // There is no way to specify more than one include directory.
            // headers '../../native/androidaudioplugin/android/include'
            // FIXME: remove this dummy headers dir hack once https://issuetracker.google.com/issues/172105145 is supported.
            headers '../../dummy-prefab-headers/include'
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                groupId = 'org.androidaudioplugin'
                version = android.defaultConfig.versionName
                from components.release
                artifactId = 'androidaudioplugin'
            }
            // Creates a Maven publication called "debug".
            debug(MavenPublication) {
                // Applies the component for the release build variant.
                groupId = 'org.androidaudioplugin'
                version = android.defaultConfig.versionName
                from components.debug
                artifactId = 'androidaudioplugin'
            }
        }
        repositories {
            maven {
                def releasesRepoUrl = "$buildDir/repos/releases"
                def snapshotsRepoUrl = "$buildDir/repos/snapshots"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test:core:1.3.0'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}
