android {
    publishing {
        multipleVariants {
            allVariants()
            withJavadocJar()
            withSourcesJar()
        }
    }
}

afterEvaluate {

    tasks.register('javadocJar', Jar) {
        archiveClassifier.set('javadoc')
    }
    tasks.register('sourceJar', Jar) {
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
        archiveClassifier.set('sources')
    }

    publishing {
        repositories {
            maven {
                name = "OSSRH"
                url = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
                credentials {
                    username = System.getenv("OSSRH_USERNAME")
                    password = System.getenv("OSSRH_PASSWORD")
                }
            }
        }

        publications {
            release(MavenPublication) {
                version = android.defaultConfig.versionName
                from components.release
            }
            debug(MavenPublication) {
                version = android.defaultConfig.versionName
                from components.debug
            }

            all {
                artifact(javadocJar)
                artifact(sourceJar)

                pom {
                    name.set(project.name)
                    description.set(android.ext.description)
                    url.set("https://github.com/atsushieno/android-audio-plugin-framework")
                    scm {
                        url.set("https://github.com/atsushieno/android-audio-plugin-framework")
                    }
                    licenses {
                        license {
                            name.set("the MIT License")
                            url.set("https://github.com/atsushieno/android-audio-plugin-framework/blob/main/LICENSE")
                        }
                    }
                    developers {
                        developer {
                            id.set("atsushieno")
                            name.set("Atsushi Eno")
                            email.set("atsushieno@gmail.com")
                        }
                    }
                }
            }
        }
    }

    // keep it as is. It is replaced by CI release builds
    signing {}
}
